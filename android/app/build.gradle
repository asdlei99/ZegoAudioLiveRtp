apply plugin: 'com.android.application'

android {
    compileSdkVersion 25

    defaultConfig {
        applicationId "com.zego.audioroomdemo"
        minSdkVersion 17
        targetSdkVersion 22
        versionCode 9
        versionName getDemoVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        ndk {
            abiFilters 'armeabi-v7a', 'x86'//, 'arm64-v8a', 'armeabi'
        }

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }

    }

    signingConfigs {
        release {
            storeFile file("../../config/your_app_key.jks")
            storePassword "your_keystore_password"
            keyAlias "your_key_alias"
            keyPassword "your_key_password"
        }
    }

    repositories {
        flatDir {
            dirs '3rdlibs'   // aar目录
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

def getDemoVersionName(){
    String versionName = "1.0"
    File version_file = file("../demo_version.txt")
    if(version_file.exists()){
        versionName = version_file.readLines()[0]
    }
    return versionName
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '25.3.0'
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation fileTree(include: ['*.jar'], dir: '3rdlibs')

    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation(name: 'zego-qrcode', ext: 'aar')
    implementation 'com.alibaba:fastjson:1.2.44'
    implementation 'com.jakewharton:butterknife:7.0.1'
    implementation 'com.android.support:appcompat-v7:25.3.1'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.tencent.bugly:crashreport:2.2.2'
    implementation 'com.tencent.bugly:nativecrashreport:3.0'
    implementation 'com.android.support:support-v4:25.3.1'
    implementation 'com.android.support:recyclerview-v7:25.3.1'
    testImplementation 'junit:junit:4.12'
}
